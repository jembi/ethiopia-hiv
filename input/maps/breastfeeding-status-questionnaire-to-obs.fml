map "http://moh.gov.et/fhir/hiv/StructureMap/BreastfeedingStatusToObservation" = "BreastfeedingStatusToObservation"

/// status = 'draft'

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source
uses "http://moh.gov.et/fhir/hiv/StructureDefinition/breastfeeding-status-observation" as target

group BreastfeedingStatusToObservation(source src : QuestionnaireResponse, target tgt : Observation) {
	src -> tgt.id = uuid() "SetObservationId";
    src -> tgt.status = 'final' "SetObservationStatus";

    src -> tgt.code = cc('http://loinc.org', '63895-7') "SetObservationCode";

    src.authored as authored -> tgt.effective = authored "SetObservationEffectiveDateTime";

    src -> tgt.category = cc('http://hl7.org/fhir/observation-category', 'exam') "SetObservationCategory";

    src.subject as patient -> tgt.subject = patient "SetObservationSubjectReference";

    src.author as author -> tgt.performer = author "SetObservationPerfomerReference";

    src.encounter as encounter -> tgt.encounter = encounter "SetObservationEncounterReference";

    src.item as item where(linkId = '1.3') then {
        item.answer first as ans then { 
            ans.value as breastfeeding -> tgt.value = create('CodeableConcept') as obsValue then {
                breastfeeding -> obsValue.coding = create('Coding') as coding then {
                    breastfeeding -> coding.system = translate(breastfeeding, 'http://moh.gov.et/fhir/hiv/ConceptMap/LOINCCodesToProprietary', 'system') "SetObservationValueCodingSystem";
                    breastfeeding -> coding.code = translate(breastfeeding, 'http://moh.gov.et/fhir/hiv/ConceptMap/LOINCCodesToProprietary', 'code') "SetObservationValueCodingCode";
                } "SetObservationValueCoding";
            };
        };
	};
}