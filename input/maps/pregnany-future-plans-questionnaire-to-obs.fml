map "http://moh.gov.et/fhir/hiv/StructureMap/PregnancyFuturePlansToObservation" = "PregnancyFuturePlansToObservation"
/// status = 'active'
/// date = '2024-06-07'

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" as source
uses "http://moh.gov.et/fhir/hiv/StructureDefinition/future-pregnancy-plans-observation" as target

group PregnancyFuturePlansToObservation(source src : QuestionnaireResponse, target tgt : Observation) {
	src -> tgt.id = uuid() "SetObservationId";
    src -> tgt.status = 'final' "SetStatusCodeValue";

    src -> tgt.code = cc('http://loinc.org', '86645-9') "SetCodeCodeableConcept";

    src.authored as authored -> tgt.effective = create('dateTime') as observationDate then {
        authored as authoredDate -> observationDate = authoredDate "SetObservationDate";
    } "ProcessObservationDate";

    src -> tgt.category = cc('http://hl7.org/fhir/observation-category', 'exam') "SetCategoryCodeableConcept";

    src.subject as patient -> tgt.subject = create('Reference') as subject then {
        patient as pat -> subject.reference = pat "SetPatientReference";
    } "ProcessPatientReference";

    src.author as author -> tgt.performer = create('Reference') as performer then {
        author as auth -> performer.reference = auth "SetAuthorReference";
    } "ProcessAuthorReference";

    src.encounter as QREncounter -> tgt.encounter = create('Reference') as ObsEncounter then {
        QREncounter as enc -> ObsEncounter.reference = enc "SetEncounterReference";
    } "ProcessAuthorReference";

    src.item as item where(linkId = 'want-to-be-pregnant') then {
        item.answer first as ans then { 
            ans.value : Coding as futurePregnancyPlans -> tgt.value = create('CodeableConcept') as futurePregnancyPlansValue then {
                futurePregnancyPlans -> futurePregnancyPlansValue = translate(futurePregnancyPlans, 'http://moh.gov.et/fhir/hiv/ConceptMap/PregnancyFuturePlansProprietaryToLOINC', 'code') "SetFuturePregnancyPlansCoding";
            };
        };
	};
}